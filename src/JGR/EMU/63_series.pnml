/*---GFX: Spritesets---*/
//Purchase Sprites
spriteset (spriteset_EMU63_purchase,	                                                            "gfx/JGR/emu63/purchase.png")               {tmpl_purchase_dh(0, 0)}
alternative_sprites(spriteset_EMU63_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,                 "gfx/JGR/emu63/purchase_32bpp.png")         {tmpl_purchase_dh_32bpp(0, 0)}

//63 series standard
spriteset (spriteset_EMU63_std_mufront,    	      	    "gfx/OTHER/mucar.png")      	{ tmpl_std(0, 0) }
spriteset (spriteset_EMU63_rev_std_mufront,    	      	"gfx/OTHER/mucar.png")      	{ tmpl_std(0, 0) }
spriteset (spriteset_EMU63_revflip_std_mufront,    	      	"gfx/OTHER/mucar.png")      	{ tmpl_std(0, 0) }
alternative_sprites(spriteset_EMU63_std_mufront,           ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,             "gfx/JGR/emu63/standard/basic_cab.png")           {tmpl_std(0, 0)}
alternative_sprites(spriteset_EMU63_rev_std_mufront,       ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,             "gfx/JGR/emu63/standard/basic_cab.png")           {tmpl_std(0, 25)}
alternative_sprites(spriteset_EMU63_revflip_std_mufront,   ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,             "gfx/JGR/emu63/standard/basic_cab.png")           {tmpl_std_rev(0, 25)}

spriteset (spriteset_EMU63_std_muback,    	      	    "gfx/OTHER/mucar.png")      	{ tmpl_std(0, 0) }
spriteset (spriteset_EMU63_rev_std_muback,    	      	"gfx/OTHER/mucar.png")      	{ tmpl_std(0, 0) }
spriteset (spriteset_EMU63_revflip_std_muback,    	      	"gfx/OTHER/mucar.png")      	{ tmpl_std(0, 0) }
alternative_sprites(spriteset_EMU63_std_muback,           ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,             "gfx/JGR/emu63/standard/basic_cab_p.png")           {tmpl_std_rev(0, 25)}
alternative_sprites(spriteset_EMU63_rev_std_muback,       ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,             "gfx/JGR/emu63/standard/basic_cab_p.png")           {tmpl_std(0, 0)}
alternative_sprites(spriteset_EMU63_revflip_std_muback,   ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,             "gfx/JGR/emu63/standard/basic_cab_p.png")           {tmpl_std_rev(0, 0)}

spriteset (spriteset_EMU63_std_mid,    	      	    "gfx/OTHER/mucar.png")      	{ tmpl_std(0, 0) }
spriteset (spriteset_EMU63_rev_std_mid,    	      	"gfx/OTHER/mucar.png")      	{ tmpl_std(0, 0) }
alternative_sprites(spriteset_EMU63_std_mid,           ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,             "gfx/JGR/emu63/standard/mucar.png")           {tmpl_std(0, 0)}
alternative_sprites(spriteset_EMU63_rev_std_mid,       ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,             "gfx/JGR/emu63/standard/mucar.png")           {tmpl_std_rev(0, 0)}

spriteset (spriteset_EMU63_std_mid_panto,    	      	    "gfx/OTHER/mucar.png")      	{ tmpl_std(0, 0) }
spriteset (spriteset_EMU63_rev_std_panto,    	      	"gfx/OTHER/mucar.png")      	{ tmpl_std(0, 0) }
alternative_sprites(spriteset_EMU63_std_mid_panto,           ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,             "gfx/JGR/emu63/standard/mucar_p.png")           {tmpl_std(0, 0)}
alternative_sprites(spriteset_EMU63_rev_std_panto,       ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,             "gfx/JGR/emu63/standard/mucar_p.png")           {tmpl_std_rev(0, 0)}
/*---END OF SECTION---*/



/*---GFX: Pantograph Positions---*/
switch(FEAT_TRAINS,SELF, sw_EMU63_gfx_std_pantograph_pos, position_in_vehid_chain%2){
  0: spriteset_EMU63_std_mid_panto;
  default: spriteset_EMU63_std_mid;
}
/*---END OF SECTION---*/



/*---GFX: Train Reversing Switches---*/
//Livery Selection
switch(FEAT_TRAINS,SELF, sw_EMU63_gfx_rev_front_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
0:spriteset_EMU63_rev_std_mufront;
}
switch(FEAT_TRAINS,SELF, sw_EMU63_gfx_revflip_front_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
0:spriteset_EMU63_revflip_std_mufront;
}

switch(FEAT_TRAINS,SELF, sw_EMU63_gfx_rev_back_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
0:spriteset_EMU63_rev_std_muback;
}
switch(FEAT_TRAINS,SELF, sw_EMU63_gfx_revflip_back_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
0:spriteset_EMU63_revflip_std_muback;
}

switch(FEAT_TRAINS,SELF, sw_EMU63_gfx_rev_mid_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
0:spriteset_EMU63_rev_std_mid;
}
switch(FEAT_TRAINS,SELF, sw_EMU63_gfx_revflip_mid_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
0:spriteset_EMU63_std_mid;
}

switch(FEAT_TRAINS,PARENT, sw_EMU63_gfx_rev_mid_panto_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
0:spriteset_EMU63_rev_std_panto;
}
switch(FEAT_TRAINS,PARENT, sw_EMU63_gfx_revflip_mid_panto_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
0:spriteset_EMU63_std_mid_panto;
}



//Check for flipped vehicles (dual_headed)
switch(FEAT_TRAINS, SELF, sw_EMU63_gfx_rev_front_flippedcheck, vehicle_is_flipped) {
  1: sw_EMU63_gfx_revflip_front_livery;
 default: sw_EMU63_gfx_rev_front_livery;
}
switch(FEAT_TRAINS, SELF, sw_EMU63_gfx_rev_back_flippedcheck, vehicle_is_flipped) {
  1: sw_EMU63_gfx_revflip_back_livery;
 default: sw_EMU63_gfx_rev_back_livery;
}
switch(FEAT_TRAINS, SELF, sw_EMU63_gfx_rev_mid_flippedcheck, vehicle_is_flipped) {
  1: sw_EMU63_gfx_revflip_mid_livery;
 default: sw_EMU63_gfx_rev_mid_livery;
}
switch(FEAT_TRAINS, SELF, sw_EMU63_gfx_rev_mid_panto_flippedcheck, vehicle_is_flipped) {
  1: sw_EMU63_gfx_revflip_mid_panto_livery;
 default: sw_EMU63_gfx_rev_mid_panto_livery;
}



//Pantograph Position Testing
switch(FEAT_TRAINS, SELF, sw_EMU63_gfx_rev_pantograph_pos2, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 8, 0x000000FF, 0x41]%2]){ //look at opposite position_in_vehID_chain_from_end
  0: sw_EMU63_gfx_rev_mid_panto_flippedcheck;
  default: sw_EMU63_gfx_rev_mid_flippedcheck;
}
switch(FEAT_TRAINS, SELF, sw_EMU63_gfx_rev_pantograph_pos, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x000000FF, 0x41]%2]){ //look at opposite position_in_vehID_chain
  0: sw_EMU63_gfx_rev_mid_panto_flippedcheck;
  default: sw_EMU63_gfx_rev_mid_flippedcheck;
}
switch(FEAT_TRAINS,SELF, sw_EMU63_gfx_rev_mid_refitcheck, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){ //look at opposite refit livery id
  0: sw_EMU63_gfx_rev_pantograph_pos;
}


//Front/Back Car Position Testing
switch(FEAT_TRAINS, SELF, sw_EMU63_gfx_rev_frontback_nomucars, 
[STORE_TEMP(position_in_consist_from_end-position_in_consist-1, 0x10F), var[0x61, 0, 0x000000FF, 0x41]%2]){ //look at opposite position_in_vehID_chain
  1:sw_EMU63_gfx_rev_front_flippedcheck;
  0:sw_EMU63_gfx_rev_back_flippedcheck;
}
switch(FEAT_TRAINS, SELF, sw_EMU63_gfx_rev_frontback_aftermucars, 
[STORE_TEMP(position_in_consist_from_end-position_in_consist-1, 0x10F), var[0x61, 0, 0x000000FF, 0x41]%2]){ //look at opposite position_in_vehID_chain
  1:sw_EMU63_gfx_rev_back_flippedcheck;
  0:sw_EMU63_gfx_rev_front_flippedcheck;
}
switch(FEAT_TRAINS, SELF, sw_EMU63_gfx_rev_frontback_mucarcheck, 
[STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F),STORE_TEMP(var[0x61, 0, 0x000000FF, 0x41],0),  //look at opposite position_in_vehID_chain
STORE_TEMP((position_in_consist_from_end-position_in_consist-1)-LOAD_TEMP(0), 0x10F),                               //look at what is ahead of the first vehicle in the vehID_chain
var[0x61, 0, 0x0000FFFF, 0xC6]]){
  1000:sw_EMU63_gfx_rev_frontback_aftermucars;
  default:sw_EMU63_gfx_rev_frontback_nomucars;
}
switch(FEAT_TRAINS, SELF, sw_EMU63_gfx_rev_frontback_sameid, 
[STORE_TEMP(position_in_consist_from_end-position_in_consist+1, 0x10F), var[0x61, 0, 0x0000FFFF, 0xC6]]){ //check ID of vehicle behind of opposite vehicle
  2061:sw_EMU63_gfx_rev_frontback_mucarcheck;
  1000:sw_EMU63_gfx_rev_front_flippedcheck;
  default:sw_EMU63_gfx_rev_back_flippedcheck;
}
switch(FEAT_TRAINS, SELF, sw_EMU63_gfx_rev_frontback, 
[STORE_TEMP(position_in_consist_from_end-position_in_consist-1, 0x10F), var[0x61, 0, 0x0000FFFF, 0xC6]]){ //check ID of vehicle infront of opposite vehicle
  2061:sw_EMU63_gfx_rev_frontback_sameid;
  1000:sw_EMU63_gfx_rev_back_flippedcheck;
  default:sw_EMU63_gfx_rev_front_flippedcheck;
}


//ID Testing
switch(FEAT_TRAINS, SELF, sw_EMU63_gfx_rev_idcheck, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xC6]]){ //check ID of opposite vehicle
  1000: sw_EMU63_gfx_rev_mid_refitcheck;
  2063: sw_EMU63_gfx_rev_frontback;
}
/*---END OF SECTION---*/



/*---GFX: Front/Back Cab Car Switches + DUAL_HEADED fix---*/

//standard normal type
switch(FEAT_TRAINS, SELF, sw_EMU63_gfx_std_frontback_flipcheck, vehicle_is_flipped){
 0: spriteset_EMU63_std_mufront;
 default: spriteset_EMU63_std_muback;
}
switch(FEAT_TRAINS, PARENT, sw_EMU63_gfx_std_frontback_revcheck, vehicle_is_reversed){
 0: sw_EMU63_gfx_std_frontback_flipcheck;
 default: sw_EMU63_gfx_rev_idcheck;
}
/*---END OF SECTION---*/



/*---GFX: Middle Car Switches---*/
//80
switch(FEAT_TRAINS, SELF, sw_EMU63_gfx_cargosubtype, cargo_subtype){
  0: sw_EMU63_gfx_std_pantograph_pos;
}

switch(FEAT_TRAINS, PARENT, sw_EMU63_gfx_mucar_revcheck_mid, vehicle_is_reversed){
    0: sw_EMU63_gfx_cargosubtype;
    default: sw_EMU63_gfx_rev_idcheck;
}
/*---END OF SECTION---*/



/*---GFX: Livery Refitting Switches---*/
//Main Switches
switch(FEAT_TRAINS, SELF, sw_EMU63_gfx_mucar,  cargo_subtype) {
  0: sw_EMU63_gfx_mucar_revcheck_mid;
}
switch(FEAT_TRAINS, SELF, sw_EMU63_gfx_main, cargo_subtype) {
  0: sw_EMU63_gfx_std_frontback_revcheck;
}

//Menu Switches
switch(FEAT_TRAINS, SELF, sw_EMU63_subtypetext_mucar, cargo_subtype) {
  0: string(LV_STANDARD);
}
switch(FEAT_TRAINS, SELF, sw_EMU63_subtypetext_main, cargo_subtype) {
  0: string(LV_STANDARD);
}

/*---END OF SECTION---*/



//Coupling
  switch(FEAT_TRAINS, SELF, sw_EMU63_attach_vehid, [vehicle_type_id == emu_63 || vehicle_type_id == mu_car]) {
  0: return string(MU_UNIQUE);
  }
  switch(FEAT_TRAINS, SELF, sw_EMU63_single_attach_vehid, [vehicle_type_id == emu_63]) {
  0: return string(MU_UNIQUE);
  }


item(FEAT_TRAINS, emu_63) {
    property {
        name:                         string(JGR_63);
        introduction_date:            date(1944, 04, 05);
        model_life:                   6;
        vehicle_life:                 8;
        reliability_decay:            22;

        cargo_capacity:               param_capacity_local;
        loading_speed:                param_loading_4D;
        cost_factor:                  16;
        running_cost_factor:          53;

        speed:                        100 km/h;
        track_type:                   NARROW_OHLE;
        power:                        420 kW;
        weight:                       37 ton;

        climates_available:           ALL_CLIMATES;
        ai_special_flag:              AI_FLAG_PASSENGER;
        sprite_id:                    SPRITE_ID_NEW_TRAIN;
        misc_flags:                   bitmask(TRAIN_FLAG_MU);
        refittable_cargo_classes:     bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost:                   0;
        dual_headed:                  1;
        ai_engine_rank:               0;

        running_cost_base:            RUNNING_COST_ELECTRIC;
        engine_class:                 ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered:    visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 4, ENABLE_WAGON_POWER);

        tractive_effort_coefficient:  0.2;
        air_drag_coefficient:         0.07;
        bitmask_vehicle_info:         0;
        length: 8;
    }
    /*Graphics and Callbacks*/
      graphics {
        default:                sw_EMU63_gfx_main;
		    purchase:               spriteset_EMU63_purchase;
		    additional_text:	      return string(JGR_40_DESC);
        cargo_subtype_text:     sw_EMU63_subtypetext_main;
        can_attach_wagon:			  sw_EMU63_attach_vehid;
        //start_stop:           ;
    }
    /*Multiple Unit Car*/
      livery_override(mu_car){
        default:                      sw_EMU63_gfx_mucar;
        cargo_capacity:               param_capacity_local;
        loading_speed:                param_loading_4D;
        cargo_subtype_text:           sw_EMU63_subtypetext_mucar;
        power: 132;
        weight: 61;
      }
}