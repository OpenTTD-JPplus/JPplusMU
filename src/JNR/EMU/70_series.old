/*---GFX: Spritesets---*/
//Purchase Sprites
spriteset (spriteset_70_purchase,	                                                     "gfx/JNR/emu70/purchase.png")               {tmpl_purchase_dh(0, 0)}
alternative_sprites(spriteset_70_purchase, ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,         "gfx/JNR/emu70/purchase_32bpp.png")         {tmpl_purchase_dh_32bpp(0, 0)}

//70 Series
spriteset (spriteset_70_front_1,    	      "gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_70_back_1,             "gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_70_front_2,          	"gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_70_back_2,             "gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_70_mid,    		        "gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_70_mid_panto,     		  "gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_70_midgr,    		      "gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }

spriteset (spriteset_70_rev_front_1,    	        "gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_70_rev_back_1,               "gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_70_rev_front_2,    	        "gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_70_rev_back_2,    		        "gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_70_rev_mid,    		          "gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_70_rev_mid_panto,    	  	  "gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_70_rev_midgr,    	      	  "gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }

spriteset (spriteset_70_revflip_front_1,    	        "gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_70_revflip_back_1,               "gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_70_revflip_front_2,    	        "gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_70_revflip_back_2,    		        "gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_70_revflip_mid,    		          "gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_70_revflip_mid_panto,    	  	  "gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_70_revflip_midgr,    	      	  "gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }

spriteset (spriteset_70_back_panto1,                        "gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_70_rev_back_panto1,                    "gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }
spriteset (spriteset_70_revflip_back_panto1,                "gfx/OTHER/mucar.png") 	{ tmpl_std(0, 0) }

alternative_sprites(spriteset_70_front_1,                 ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu70/70.png")             {tmpl_std(0, 0)}
alternative_sprites(spriteset_70_back_1,                  ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu70/70.png")             {tmpl_std_rev(0, 25)}
alternative_sprites(spriteset_70_front_2,                 ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu70/70.png")             {tmpl_std(0, 125)}
alternative_sprites(spriteset_70_back_2,                  ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu70/70.png")             {tmpl_std_rev(0, 150)}
alternative_sprites(spriteset_70_mid,                     ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu70/70.png")             {tmpl_std(0, 75)}
alternative_sprites(spriteset_70_mid_panto,               ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu70/70.png")             {tmpl_std(0, 50)}
alternative_sprites(spriteset_70_midgr,                   ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu70/70.png")             {tmpl_std(0, 100)}

alternative_sprites(spriteset_70_rev_front_1,                 ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu70/70.png")             {tmpl_std(0, 25)}
alternative_sprites(spriteset_70_rev_back_1,                  ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu70/70.png")             {tmpl_std(0, 0)}
alternative_sprites(spriteset_70_rev_front_2,                 ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu70/70.png")             {tmpl_std(0, 150)}
alternative_sprites(spriteset_70_rev_back_2,                  ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu70/70.png")             {tmpl_std(0, 125)}
alternative_sprites(spriteset_70_rev_mid,                     ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu70/70.png")             {tmpl_std_rev(0, 75)}
alternative_sprites(spriteset_70_rev_mid_panto,               ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu70/70.png")             {tmpl_std_rev(0, 50)}
alternative_sprites(spriteset_70_rev_midgr,                   ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu70/70.png")             {tmpl_std_rev(0, 100)}

alternative_sprites(spriteset_70_revflip_front_1,             ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu70/70.png")             {tmpl_std_rev(0, 25)}
alternative_sprites(spriteset_70_revflip_back_1,              ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu70/70.png")             {tmpl_std_rev(0, 0)}
alternative_sprites(spriteset_70_revflip_front_2,             ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu70/70.png")             {tmpl_std_rev(0, 150)}
alternative_sprites(spriteset_70_revflip_back_2,              ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu70/70.png")             {tmpl_std_rev(0, 125)}
alternative_sprites(spriteset_70_revflip_mid,                 ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu70/70.png")             {tmpl_std(0, 75)}
alternative_sprites(spriteset_70_revflip_mid_panto,           ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu70/70.png")             {tmpl_std(0, 50)}
alternative_sprites(spriteset_70_revflip_midgr,               ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu70/70.png")             {tmpl_std(0, 100)}

alternative_sprites(spriteset_70_back_panto1,                          ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu70/70-cabpantographs.png")             {tmpl_std_rev(0, 25)}
alternative_sprites(spriteset_70_rev_back_panto1,                      ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu70/70-cabpantographs.png")             {tmpl_std(0, 0)}
alternative_sprites(spriteset_70_revflip_back_panto1,                  ZOOM_LEVEL_NORMAL, BIT_DEPTH_32BPP,       "gfx/JNR/emu70/70-cabpantographs.png")             {tmpl_std_rev(0, 0)}
/*---END OF SECTION---*/



/*---GFX: Pantograph Positions---*/
//70
switch(FEAT_TRAINS,SELF, sw_70_gfx_pantograph_mid, position_in_vehid_chain_from_end%2){
  0: spriteset_70_mid_panto;
  default:spriteset_70_mid;
}
switch(FEAT_TRAINS,SELF, sw_70_gfx_pantograph_midgr1, position_in_vehid_chain){
  1: spriteset_70_mid;
  3: spriteset_70_midgr;
  default:spriteset_70_mid_panto;
}
switch(FEAT_TRAINS,SELF, sw_70_gfx_pantograph_midgr2, position_in_vehid_chain){
  1: spriteset_70_mid;
  2: spriteset_70_mid_panto;
  3..4: spriteset_70_midgr;
  default:sw_70_gfx_pantograph_mid;
}
switch(FEAT_TRAINS,SELF, sw_70_gfx_pantograph_middlesw, num_vehs_in_vehid_chain){
  0..4: sw_70_gfx_pantograph_mid;
  5: sw_70_gfx_pantograph_midgr1;
  default: sw_70_gfx_pantograph_midgr2;
}
switch(FEAT_TRAINS,SELF, sw_70_gfx_pantograph_pos_back, position_in_vehid_chain_from_end){
  0: spriteset_70_mid_panto;
  default:sw_70_gfx_pantograph_middlesw;
}
switch(FEAT_TRAINS,SELF, sw_70_gfx_pantograph_pos_front, position_in_vehid_chain){
  0: spriteset_70_mid_panto;
  default:sw_70_gfx_pantograph_pos_back;
}
switch(FEAT_TRAINS,SELF, sw_70_gfx_pantograph_pos, num_vehs_in_vehid_chain){
  default: sw_70_gfx_pantograph_pos_front;
}
/*---END OF SECTION---*/



/*---GFX: Train Reversing Switches---*/
//Livery Selection
switch(FEAT_TRAINS,SELF, sw_70_gfx_rev_front_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
0:spriteset_70_rev_front_1;
}
switch(FEAT_TRAINS,SELF, sw_70_gfx_revflip_front_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
0:spriteset_70_revflip_front_1;
}

switch(FEAT_TRAINS,SELF, sw_70_gfx_rev_back_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
0:spriteset_70_rev_back_1;
}
switch(FEAT_TRAINS,SELF, sw_70_gfx_revflip_back_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
0:spriteset_70_revflip_back_1;
}

switch(FEAT_TRAINS,SELF, sw_70_gfx_rev_mid_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
0:spriteset_70_rev_mid;
}
switch(FEAT_TRAINS,SELF, sw_70_gfx_revflip_mid_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
0:spriteset_70_revflip_mid;
}

switch(FEAT_TRAINS,PARENT, sw_70_gfx_rev_mid_panto_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
0:spriteset_70_rev_mid_panto;
}
switch(FEAT_TRAINS,PARENT, sw_70_gfx_revflip_mid_panto_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
0:spriteset_70_revflip_mid_panto;
}

switch(FEAT_TRAINS,SELF, sw_70_gfx_rev_midgr_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
0:spriteset_70_rev_midgr;
}
switch(FEAT_TRAINS,SELF, sw_70_gfx_revflip_midgr_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
0:spriteset_70_revflip_midgr;
}

switch(FEAT_TRAINS,SELF, sw_70_gfx_rev_back_panto_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
0:spriteset_70_rev_back_panto1;
}
switch(FEAT_TRAINS,SELF, sw_70_gfx_revflip_back_panto_livery, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){
0:spriteset_70_revflip_back_panto1;
}


//Check for flipped vehicles (dual_headed)
switch(FEAT_TRAINS, SELF, sw_70_gfx_rev_front_flippedcheck, vehicle_is_flipped) {
  1: sw_70_gfx_revflip_front_livery;
 default: sw_70_gfx_rev_front_livery;
}
switch(FEAT_TRAINS, SELF, sw_70_gfx_rev_back_flippedcheck, vehicle_is_flipped) {
  1: sw_70_gfx_revflip_back_livery;
 default: sw_70_gfx_rev_back_livery;
}
switch(FEAT_TRAINS, SELF, sw_70_gfx_rev_mid_flippedcheck, vehicle_is_flipped) {
  1: sw_70_gfx_revflip_mid_livery;
 default: sw_70_gfx_rev_mid_livery;
}
switch(FEAT_TRAINS, SELF, sw_70_gfx_rev_mid_panto_flippedcheck, vehicle_is_flipped) {
  1: sw_70_gfx_revflip_mid_panto_livery;
 default: sw_70_gfx_rev_mid_panto_livery;
}
switch(FEAT_TRAINS, SELF, sw_70_gfx_rev_midgr_flippedcheck, vehicle_is_flipped) {
  1: sw_70_gfx_revflip_midgr_livery;
 default: sw_70_gfx_rev_midgr_livery;
}
switch(FEAT_TRAINS, SELF, sw_70_gfx_rev_back_panto_flippedcheck, vehicle_is_flipped) {
  1: sw_70_gfx_revflip_back_panto_livery;
 default: sw_70_gfx_rev_back_panto_livery;
}


//Pantograph Position Testing
switch(FEAT_TRAINS, SELF, sw_70_gfx_rev_pantograph_pos_mid, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 8, 0x000000FF, 0x41]%2]){ //look at opposite position_in_vehID_chain_from_end
  0: sw_70_gfx_rev_mid_panto_flippedcheck;
  default: sw_70_gfx_rev_mid_flippedcheck;
}
switch(FEAT_TRAINS, SELF, sw_70_gfx_rev_pantograph_pos_midgr1, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x000000FF, 0x41]]){ //look at opposite position_in_vehID_chain
  1: sw_70_gfx_rev_mid_flippedcheck;
  3: sw_70_gfx_rev_midgr_flippedcheck;
  default: sw_70_gfx_rev_mid_panto_flippedcheck;
}
switch(FEAT_TRAINS, SELF, sw_70_gfx_rev_pantograph_pos_midgr2, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x000000FF, 0x41]]){ //look at opposite position_in_vehID_chain
  1: sw_70_gfx_rev_mid_flippedcheck;
  2: sw_70_gfx_rev_mid_panto_flippedcheck;
  3..4: sw_70_gfx_rev_midgr_flippedcheck;
  default: sw_70_gfx_rev_pantograph_pos_mid;
}
switch(FEAT_TRAINS, SELF, sw_70_gfx_rev_pantograph_pos_middlesw, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 16, 0x000000FF, 0x41]]){ //look at opposite num_vehs_in_vehid_chain
  0..4: sw_70_gfx_rev_pantograph_pos_mid;
  5: sw_70_gfx_rev_pantograph_pos_midgr1;
  default: sw_70_gfx_rev_pantograph_pos_midgr2;
}
switch(FEAT_TRAINS, SELF, sw_70_gfx_rev_pantograph_pos_back, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 8, 0x000000FF, 0x41]]){ //look at opposite position_in_vehID_chain_from_end
  0: sw_70_gfx_rev_mid_panto_flippedcheck;
  default: sw_70_gfx_rev_pantograph_pos_middlesw;
}
switch(FEAT_TRAINS, SELF, sw_70_gfx_rev_pantograph_pos_front, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x000000FF, 0x41]]){ //look at opposite position_in_vehID_chain
  0: sw_70_gfx_rev_mid_panto_flippedcheck;
  default: sw_70_gfx_rev_pantograph_pos_back;
}
switch(FEAT_TRAINS, SELF, sw_70_gfx_rev_pantograph_pos, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 16, 0x000000FF, 0x41]]){ //look at opposite num_vehs_in_vehid_chain
  default: sw_70_gfx_rev_pantograph_pos_front;
}

switch(FEAT_TRAINS,SELF, sw_70_gfx_rev_mid_refitcheck, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xF2]]){ //look at opposite refit livery id
  0: sw_70_gfx_rev_pantograph_pos_front;
}


//Back Car Pantograph Testing
switch(FEAT_TRAINS, SELF, sw_70_gfx_rev_back_pantocheck, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x000000FF, 0x41]]){ //look at opposite position_in_vehID_chain
 0: sw_70_gfx_rev_back_flippedcheck;
 default: sw_70_gfx_rev_back_panto_flippedcheck;
}


//Front/Back Car Position Testing
switch(FEAT_TRAINS, SELF, sw_70_gfx_rev_frontback_nomucars, 
[STORE_TEMP(position_in_consist_from_end-position_in_consist-1, 0x10F), var[0x61, 0, 0x000000FF, 0x41]%2]){ //look at opposite position_in_vehID_chain
  1:sw_70_gfx_rev_front_flippedcheck;
  0:sw_70_gfx_rev_back_pantocheck;
}
switch(FEAT_TRAINS, SELF, sw_70_gfx_rev_frontback_aftermucars, 
[STORE_TEMP(position_in_consist_from_end-position_in_consist-1, 0x10F), var[0x61, 0, 0x000000FF, 0x41]%2]){ //look at opposite position_in_vehID_chain
  1:sw_70_gfx_rev_back_pantocheck;
  0:sw_70_gfx_rev_front_flippedcheck;
}
switch(FEAT_TRAINS, SELF, sw_70_gfx_rev_frontback_mucarcheck, 
[STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F),STORE_TEMP(var[0x61, 0, 0x000000FF, 0x41],0),  //look at opposite position_in_vehID_chain
STORE_TEMP((position_in_consist_from_end-position_in_consist-1)-LOAD_TEMP(0), 0x10F),                               //look at what is ahead of the first vehicle in the vehID_chain
var[0x61, 0, 0x0000FFFF, 0xC6]]){
  1000:sw_70_gfx_rev_frontback_aftermucars;
  default:sw_70_gfx_rev_frontback_nomucars;
}
switch(FEAT_TRAINS, SELF, sw_70_gfx_rev_frontback_sameid, 
[STORE_TEMP(position_in_consist_from_end-position_in_consist+1, 0x10F), var[0x61, 0, 0x0000FFFF, 0xC6]]){ //check ID of vehicle behind of opposite vehicle
  2028:sw_70_gfx_rev_frontback_mucarcheck;
  1000:sw_70_gfx_rev_front_flippedcheck;
  default:sw_70_gfx_rev_back_pantocheck;
}
switch(FEAT_TRAINS, SELF, sw_70_gfx_rev_frontback, 
[STORE_TEMP(position_in_consist_from_end-position_in_consist-1, 0x10F), var[0x61, 0, 0x0000FFFF, 0xC6]]){ //check ID of vehicle infront of opposite vehicle
  2028:sw_70_gfx_rev_frontback_sameid;
  1000:sw_70_gfx_rev_back_pantocheck;
  default:sw_70_gfx_rev_front_flippedcheck;
}


//ID Testing
switch(FEAT_TRAINS, SELF, sw_70_gfx_rev_idcheck, [STORE_TEMP(position_in_consist_from_end-position_in_consist, 0x10F), var[0x61, 0, 0x0000FFFF, 0xC6]]){ //check ID of opposite vehicle
  1000: sw_70_gfx_rev_mid_refitcheck;
  2028: sw_70_gfx_rev_frontback;
}
/*---END OF SECTION---*/



/*---GFX: Front/Back Cab Car Switches + DUAL_HEADED fix---*/
//70 series
switch(FEAT_TRAINS, SELF, sw_70_gfx_pantocheck, position_in_vehid_chain){
 0: spriteset_70_back_1;
 default: spriteset_70_back_panto1;
}
switch(FEAT_TRAINS, SELF, sw_70_gfx_frontback_flipcheck, vehicle_is_flipped){
 0: spriteset_70_front_1;
 default: sw_70_gfx_pantocheck;
}
switch(FEAT_TRAINS, PARENT, sw_70_gfx_frontback_revcheck, vehicle_is_reversed){
 0: sw_70_gfx_frontback_flipcheck;
 default: sw_70_gfx_rev_idcheck;
}
/*---END OF SECTION---*/



/*---GFX: Middle Car Switches---*/
//70
switch(FEAT_TRAINS, SELF, sw_70_gfx_cargosubtype, cargo_subtype){
  0: sw_70_gfx_pantograph_pos;
}

switch(FEAT_TRAINS, PARENT, sw_70_gfx_mucar_revcheck_mid, vehicle_is_reversed){
    0: sw_70_gfx_cargosubtype;
    default: sw_70_gfx_rev_idcheck;
}
/*---END OF SECTION---*/



/*---GFX: Livery Refitting Switches---*/
//Main Switches
switch(FEAT_TRAINS, SELF, sw_70_gfx_mucar,  cargo_subtype) {
  default: sw_70_gfx_mucar_revcheck_mid;
}
switch(FEAT_TRAINS, SELF, sw_70_gfx_main,  cargo_subtype) {
  0: sw_70_gfx_frontback_revcheck;
}


//Menu Switches
switch(FEAT_TRAINS, SELF, sw_70_subtypetext_mucar, cargo_subtype) {
  0: string(LV_YOKOSUKA);
}
switch(FEAT_TRAINS, SELF, sw_70_subtypetext_main, cargo_subtype) {
  0: string(LV_YOKOSUKA);
}
/*---END OF SECTION---*/



//Coupling
  switch(FEAT_TRAINS, SELF, sw_70_attach_vehid, [vehicle_type_id == emu_70 || vehicle_type_id == mu_car]) {
  0: return string(MU_UNIQUE);
  }



item(FEAT_TRAINS, emu_70) {
    property {
        name:                         string(JNR_70);
        introduction_date:            date(1951, 11, 12);
        model_life:                   7;
        vehicle_life:                 26;
        reliability_decay:            11;

        cargo_capacity:               param_capacity_local;
        loading_speed:                param_loading_3D;
        cost_factor:                  19;
        running_cost_factor:          49;

        speed:                        100 km/h;
        track_type:                   NARROW_OHLE;
        power:                        568 kW;
        weight:                       46 ton;

        climates_available:           ALL_CLIMATES;
        ai_special_flag:              AI_FLAG_PASSENGER;
        sprite_id:                    SPRITE_ID_NEW_TRAIN;
        misc_flags:                   bitmask(TRAIN_FLAG_MU);
        refittable_cargo_classes:     bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes: bitmask();
        refit_cost:                   0;
        dual_headed:                  1;
        ai_engine_rank:               0;

        running_cost_base:            RUNNING_COST_ELECTRIC;
        engine_class:                 ENGINE_CLASS_ELECTRIC;
        visual_effect_and_powered:    visual_effect_and_powered(VISUAL_EFFECT_ELECTRIC, 4, ENABLE_WAGON_POWER);

        tractive_effort_coefficient:  0.2;
        air_drag_coefficient:         0.07;
        bitmask_vehicle_info:         0;
        length: 8;
    }
    /*Graphics and Callbacks*/
      graphics {
        default:                      sw_70_gfx_main;
		    purchase:                 spriteset_70_purchase;
		    additional_text:	      return string(JNR_70_DESC);
        cargo_subtype_text:           sw_70_subtypetext_main;
        can_attach_wagon:			  sw_70_attach_vehid;
        //start_stop:                   ;
    }
    /*Multiple Unit Car*/
      livery_override(mu_car){
        default:                      sw_70_gfx_mucar;
        cargo_capacity:               param_capacity_local;
        loading_speed:                param_loading_3D;
        cargo_subtype_text:           sw_70_subtypetext_mucar;
        power: 568/2;
        weight: 32;
      }

}